#
# Copyright 2016-2017 The Reaktivity Project
#
# The Reaktivity Project licenses this file to you under the Apache License,
# version 2.0 (the "License"); you may not use this file except in compliance
# with the License. You may obtain a copy of the License at:
#
#   http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS, WITHOUT
# WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. See the
# License for the specific language governing permissions and limitations
# under the License.
#

property newNetworkRouteRef  ${nuklei:newReferenceId()} # external

property networkConnect "nukleus://kafka/streams/source"
property networkConnectWindow 8192

property newMetadataRequestId1 ${kafka:newRequestId()}
property newMetadataRequestId2 ${kafka:newRequestId()}
property newDescribeRequestId ${kafka:newRequestId()}

connect await ROUTED_SERVER
        ${networkConnect}
  option nukleus:route ${newNetworkRouteRef}
  option nukleus:window ${networkConnectWindow} 
  option nukleus:transmission "duplex"
  option nukleus:byteorder "network"

connected

# First attempt leader not available (in mid failover)

write 21 # Size int32
write 0x03s # ApiKey int16 (Metadata)
write 0x05s # ApiVersion int16
write ${newMetadataRequestId1} # CorrelationId int32
write -1s # ClientId string (null)
write 1 # [TopicName] array length
  write 4s "test"
write [0x00] # allow_auto_topic_creation (boolean)

read 126 # Size int32
read ${newMetadataRequestId1} # CorrelationId int32
read [0..4] # throttle_time_ms int32
read 2      # brokers array length
  read 1    # broker id
  read 7s "broker1"
  read 9093 # port int32
  read -1s  # rack string (null)

  read 2
  read 7s "broker2"
  read 9093
  read -1s
read 9s "cluster 1"
read 1 # controller broker id
read 1 # topic array length
  read 0s # error code
  read 4s "test"
  read [0x00] # is_internal boolean
  read 2 # partition array length
    read 0s # error code
    read 0 # partition
    read 1 # leader
    read 0 # replicas array (empty)
    read -1 # isr array (null)
    read 0 # offline replicas array (empty)

    read 5s # error code LeaderNotAvailable
    read 1 # partition
    read -1 # leader
    read 0 # replicas array (empty)
    read -1 # isr array (null)
    read 0 # offline replicas array (empty)
    
# Second attempt succeeds 

write 21 # Size int32
write 0x03s # ApiKey int16 (Metadata)
write 0x05s # ApiVersion int16
write ${newMetadataRequestId2} # CorrelationId int32
write -1s # ClientId string (null)
write 1 # [TopicName] array length
  write 4s "test"
write [0x00] # allow_auto_topic_creation (boolean)

read 126 # Size int32
read ${newMetadataRequestId2} # CorrelationId int32 # 151
read [0..4] # throttle_time_ms int32
read 2      # brokers array length
  read 1    # broker id # 12 + 127 = 139
  read 7s "broker1"
  read 9093 # port int32
  read -1s  # rack string (null)

  read 2    # 12 + 115 = 127
  read 7s "broker2"
  read 9093
  read -1s
read 9s "cluster 1"  # 125
read 1 # controller broker id
read 1 # topic array length
  read 0s # error code   57 + 58 = 115
  read 4s "test"
  read [0x00] # is_internal boolean
  read 2 # partition array length
    read 0s # error code
    read 0 # partition
    read 1 # leader
    read 0 # replicas array (empty)
    read -1 # isr array (null)
    read 0 # offline replicas array (empty)

    read 0s # error code
    read 1 # partition
    read 2 # leader
    read 0 # replicas array (empty)
    read -1 # isr array (null)
    read 0 # offline replicas array (empty)

write 41        # Size int32
write 32s       # ApiKey int16 (DescribeConfigs)
write 0s        # ApiVersion int16
write ${newDescribeRequestId} # CorrelationId int32
write -1s       # ClientId string (null)
write 1         # resources count
write [0x02]    # resource type (topic is 2, from org/apache/kafka/common/resource/ResourceType.java)
write 4s "test" # topic name
write 1         # config names count
write 14s "cleanup.policy"  # config name

read 54         # Size int32
read ${newDescribeRequestId} # CorrelationId int32
read [0..4]     # throttle_time_ms int32
read 1          # resources count
read 0s         # error code
read -1s        # error message
read [0x02]     # resource type (topic)
read 4s "test"  # topic name
read 1          # config entries count
read 14s "cleanup.policy"   # config name
read 6s "delete"            # config value
read [0x00]     # read_only boolean
read [0x01]     # is_default boolean
read [0x00]     # is_sensitive boolean

