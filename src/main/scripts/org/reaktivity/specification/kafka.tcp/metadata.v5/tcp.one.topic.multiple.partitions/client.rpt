#
# Copyright 2016-2017 The Reaktivity Project
#
# The Reaktivity Project licenses this file to you under the Apache License,
# version 2.0 (the "License"); you may not use this file except in compliance
# with the License. You may obtain a copy of the License at:
#
#   http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS, WITHOUT
# WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. See the
# License for the specific language governing permissions and limitations
# under the License.
#

# This script works against a kafka broker listening on the following address:
property brokerUrl "tcp://docker:9092"

property newMetadataRequestId ${kafka:newRequestId()}

connect ${brokerUrl}
connected

write 21        # Size int32
write 0x03s     # ApiKey int16 (Metadata)
write 0x05s     # ApiVersion int16
write ${newMetadataRequestId} # CorrelationId int32
write -1s       # ClientId string (null)
write 1         # [TopicName] array length
  write 4s "test"
write [0x00]    # allow_auto_topic_creation (boolean)

read [0..4] # 134        # Size int32
read ${newMetadataRequestId} # CorrelationId int32
read [0..4]     # throttle_time_ms int32
read 1          # brokers array length
  read 1001     # broker id
  read 5s "kafka"
  read 9092     # port int32
  read -1s      # rack string (null)
read (short:clusterNameLength)
read [0..${clusterNameLength}] # cluster name
read 1001       # controller broker id
read 1          # topic array length
  read 0s       # error code
  read 4s "test"
  read [0x00]   # is_internal boolean
  read 2        # partition array length
    read 0s     # error code
    read 1      # partition
    read 1001   # leader
    read 1      # replicas array 
    read 1001   # node having a replica
    read 1      # isr array
    read 1001   # node having an in sync replica
    read 0      # offline replicas array (empty)

    read 0s     # error code
    read 0      # partition
    read 1001   # leader
    read 1      # replicas array
    read 1001   # node having a replica
    read 1      # isr array
    read 1001   # node having an in sync replica
    read 0      # offline replicas array (empty)
